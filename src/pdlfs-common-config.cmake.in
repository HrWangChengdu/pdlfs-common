
set (PDLFS_REQUIRED_PACKAGES "@PDLFS_REQUIRED_PACKAGES@")
set (PDLFS_REQUIRED_XPKGIMPORTS "@PDLFS_REQUIRED_XPKGIMPORTS@")
set (PDLFS_EXTRA_COMPONENTS "@PDLFS_EXTRA_COMPONENTS@")

set (PDLFS_ALL_COMPONENTS ${PDLFS_REQUIRED_PACKAGES}
                          ${PDLFS_REQUIRED_XPKGIMPORTS}
                          ${PDLFS_EXTRA_COMPONENTS})

include (CMakeFindDependencyMacro)
include ("${CMAKE_CURRENT_LIST_DIR}/xpkg-import.cmake")

foreach (lcv ${PDLFS_REQUIRED_PACKAGES})
    find_dependency (${lcv})
endforeach ()

foreach (lcv ${PDLFS_REQUIRED_XPKGIMPORTS})
    xpkg_import_module (${lcv} REQUIRED ${lcv})
endforeach ()

# XXX: as long as we hide mercury behind our API we don't need to worry
# about target_include_directories our lib with ${MERCURY_INCLUDE_DIR}

# check for user requested components (which we map to linked in packages)
foreach (comp ${@PDLFS_NAME@_FIND_COMPONENTS})
    if (NOT ";${PDLFS_ALL_COMPONENTS};" MATCHES ";${comp};")
        set(@PDLFS_NAME@_FOUND False)
        set(@PDLFS_NAME@_NOTFOUND_MESSAGE 
            "Specified unsupported component: ${comp}")
    endif()
endforeach()

include ("${CMAKE_CURRENT_LIST_DIR}/@PDLFS_NAME@-targets.cmake")

